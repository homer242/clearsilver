dnl Process this file with autoconf to produce a configure script.
AC_INIT(clearsilver, 0.10.6, avd@patatrac.info)
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE([foreign])
LT_INIT

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_LN_S
AC_CHECK_PROGS(AR, ar aal, ar)
AC_PROG_MAKE_SET
AC_PROG_INSTALL

dnl Checks for Neotonic Paths
AC_MSG_CHECKING(for Neotonic Paths)
if test -d /neo/opt/include; then
  AC_MSG_RESULT(found)
  CPPFLAGS="$CPPFLAGS -I/neo/opt/include"
  LDFLAGS="$LDFLAGS -L/neo/opt/lib"
else
  AC_MSG_RESULT(not found)
fi

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h stdarg.h varargs.h limits.h strings.h sys/ioctl.h sys/time.h unistd.h features.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
AC_CHECK_FUNCS(gettimeofday mktime putenv strerror strspn strtod strtol strtoul)
AC_CHECK_FUNCS(random rand drand48)

dnl Check for locks
cs_cv_have_lockf=no
AC_CHECK_FUNC(lockf, [
  cs_cv_have_lockf=yes

  cs_cv_have_pthread=no
  AC_CHECK_HEADER(pthread.h, [cs_cv_have_pthread=yes])
])

dnl Mingw32 ?
AC_MINGW32()
if test "x$MINGW32" = "xyes"; then
  CPPFLAGS="$CPPFLAGS -D__WINDOWS_GCC__"
fi

dnl Check for snprintf and vsnprintf
cs_cv_have_snprintf=no
AC_CHECK_FUNC(snprintf, [cs_cv_have_snprintf=yes])

cs_cv_have_vsnprintf=no
AC_CHECK_FUNC(vsnprintf, [cs_cv_have_vsnprintf=yes])

dnl Check for missing re-entrant functions
cs_cv_missing=no
cs_cv_need_reentrant=no

cs_cv_have_localtime_r=no
AC_CHECK_FUNCS(localtime_r, [
  AC_MSG_CHECKING(whether localtime_r is declared)
  AC_EGREP_CPP(localtime_r,[
#include <time.h>],[
    cs_cv_have_localtime_r=yes
    AC_MSG_RESULT(yes)],[
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING(whether localtime_r with -D_REENTRANT is declared)
    AC_EGREP_CPP(localtime_r,[
#define _REENTRANT
#include <time.h>],[
      cs_cv_need_reentrant=yes
      AC_MSG_RESULT(yes)],[
      cs_cv_missing=yes
      AC_MSG_RESULT(no)])])], [cs_cv_missing=yes])

cs_cv_have_gmtime_r=no
AC_CHECK_FUNCS(gmtime_r, [
  AC_MSG_CHECKING(whether gmtime_r is declared)
  AC_EGREP_CPP(gmtime_r,[
#include <time.h>],[
    cs_cv_have_gmtime_r=yes
    AC_MSG_RESULT(yes)],[
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING(whether gmtime_r with -D_REENTRANT is declared)
    AC_EGREP_CPP(gmtime_r,[
#define _REENTRANT
#include <time.h>],[
      cs_cv_need_reentrant=yes
      AC_MSG_RESULT(yes)],[
      cs_cv_missing=yes
      AC_MSG_RESULT(no)])])], [cs_cv_missing=yes])

cs_cv_have_strtok_r=no
AC_CHECK_FUNCS(strtok_r, [
  AC_MSG_CHECKING(whether strtok_r is declared)
  AC_EGREP_CPP(strtok_r,[
#include <string.h>],[
    cs_cv_have_strtok_r=yes
    AC_MSG_RESULT(yes)],[
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING(whether strtok_r with -D_REENTRANT is declared)
    AC_EGREP_CPP(strtok_r,[
#define _REENTRANT
#include <string.h>],[
      cs_cv_need_reentrant=yes
      AC_MSG_RESULT(yes)],[
      cs_cv_missing=yes
      AC_MSG_RESULT(no)])])], [cs_cv_missing=yes])

cs_cv_have_mkstemp=no
AC_CHECK_FUNC(mkstemp, [cs_cv_have_mkstemp=yes], [cs_cv_missing=yes])
if test "x$cs_cv_need_reentrant" = "xyes"; then
  CPPFLAGS="$CPPFLAGS -D_REENTRANT"
fi

cs_cv_have_regex=no
AC_CHECK_FUNC(regexec, [cs_cv_have_regex=yes])
if test "x$cs_cv_have_regex" = "xno"; then
  CPPFLAGS="$CPPFLAGS -I\$(NEOTONIC_ROOT)/util/regex"
fi

dnl wdb option
AC_ARG_ENABLE(wdb, [  --enable-wdb   Enables building of wdb])

if test "x$enable_wdb" = "xyes"; then
  AC_MSG_RESULT(Enabling wdb)
  AC_SUBST(cs_enable_wdb, 1)
fi

dnl html compression option
AC_ARG_ENABLE(compression, [  --enable-compression   Enables HTML Compression support])

if test "x$enable_compression" = "xyes"; then
  AC_SEARCH_LIBS([deflate], [z])
  AC_MSG_RESULT(Enabling HTML Compression support)
  AC_SUBST(cs_enable_html_compression, 1)
  LIBS="$LIBS -lz"
fi

dnl remote debug option
AC_ARG_ENABLE(remote-debugger, [  --enable-remote-debugger   Enables remote X CGI debugging])

if test "x$enable_remote_debugger" = "xyes"; then
  AC_MSG_RESULT(Enabling CGI X Remote debugger)
  AC_SUBST(cs_enable_remote_debug, 1)
fi

dnl gettext option
AC_ARG_ENABLE(gettext, [  --enable-gettext   Enables gettext message translation])

if test "x$enable_gettext" = "xyes"; then
  AC_CHECK_FUNC(gettext, [cs_cv_libintl=yes], [cs_cv_libintl=no])
  AC_CHECK_HEADER(libintl.h, [cs_cv_libintl=yes], [cs_cv_libintl=no])

  if test "x$cs_cv_libintl" = "xyes"; then
    AC_MSG_RESULT(Enabling gettext message translation)
    AC_SUBST(cs_enable_gettext, 1)
  else
    AC_MSG_ERROR(not found gettext function or libintl.h header)
  fi
fi

dnl apache options
AC_ARG_ENABLE(apache, [  --enable-apache   Enables building of apache 1.3.x module])
AC_ARG_WITH(apache, [  --with-apache=path   Set location of Apache installation], [cs_cv_apache_path="$withval"], [cs_cv_apache_path=])

if test "x$enable_apache" = "xyes"; then
  AC_MSG_RESULT(Enabling Apache 1.3.x Module)
  AC_MSG_CHECKING(for apache apxs)

  apxs_path=no
  apache_search_path="$cs_cv_apache_path /neo/opt /usr/local /usr"
  for path in $apache_search_path; do
    if test -x $path/httpd/bin/apxs; then
      apxs_path=$path/httpd/bin/apxs
      httpd_path=$path/httpd/bin/httpd
      break
    fi
    if test -x $path/httpd/sbin/apxs; then
      apxs_path=$path/httpd/sbin/apxs
      httpd_path=$path/httpd/sbin/httpd
      break
    fi
    if test -x $path/bin/apxs; then
      apxs_path=$path/bin/apxs
      httpd_path=$path/bin/httpd
      break
    fi
    if test -x $path/sbin/apxs; then
      apxs_path=$path/sbin/apxs
      httpd_path=$path/sbin/httpd
      break
    fi
  done
  if test "x$apxs_path" = "xno"; then
    AC_MSG_ERROR(not found)
  else
    AC_MSG_RESULT(found $apxs_path)
    AC_MSG_CHECKING(for apache 1.3.x)
    changequote(<<, >>)dnl
    apache_version="`$httpd_path -v | grep 'Server version' | sed -e 's/.*Apache\/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1/'`"
    apache_major_version=`echo $apache_version | sed -e 's/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/\1/'`
    apache_minor_version=`echo $apache_version | sed -e 's/.*\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\).*/\2/'`
    changequote([, ])dnl
    if test "$apache_major_version" = "1" -a "$apache_minor_version" = "3"; then
      AC_MSG_RESULT(found $apache_version)
      APXS_PATH="$apxs_path"
      BUILD_WRAPPERS="$BUILD_WRAPPERS mod_ecs"
    else
      AC_MSG_RESULT(found $apache_version - disabling module build)
    fi
  fi
fi

dnl python options
AC_ARG_ENABLE(python, [  --enable-python   Enables building of python module])
AC_ARG_WITH(python, [  --with-python=path   Set location of Python Interpreter], [cs_cv_python_path="$withval"], [cs_cv_python_path=no])

if test "x$enable_python" = "xyes"; then
  AC_MSG_RESULT(Enabling python module)
  AC_MSG_CHECKING(for python includes)
  python_inc=no
  python_lib=no
  python_search_path="/neo/opt /usr/local /usr /c"
  python_versions="2.4 2.3 2.2 2.1 2.0 1.5 24 23 22 21 20 15"
  if test $cs_cv_python_path != "no" -a -x $cs_cv_python_path; then
    python_bin=$cs_cv_python_path
    vers=`$python_bin -c "import sys; print sys.version[[:3]]"`
    py_inst_dir=`$python_bin -c "import sys; print sys.exec_prefix"`
    python_inc=$py_inst_dir/include/python$vers
    python_lib="-L$py_inst_dir/lib/python$vers/config -lpython$vers"
    python_site=$py_inst_dir/lib/python$vers/site-packages
  else
    for vers in $python_versions; do
      for path in $python_search_path; do
        if test -x $path/bin/python$vers; then
	  python_bin=$path/bin/python$vers
	  major_vers=`echo $vers | cut -b 1`
	  if test $major_vers -ge 2; then
	      python_base=`$python_bin -c "import sys, os; print os.path.dirname([[x for x in sys.path if x.find('site-packages') != -1]][[0]])"`
	  else
	      python_base=`$python_bin -c "import site, os; print os.path.dirname(site.sitedirs[[0]])"`
	  fi
	  if test -d $python_base; then
	      python_lib="-L$python_base/config -lpython$vers"
	      python_site=$python_base/site-packages
	  fi
	fi
	if test -f $path/include/python$vers/Python.h; then
	  python_inc=$path/include/python$vers
        fi
	if test "x$python_lib" = "xno"; then
	    if test -d $path/lib/python$vers; then
	      python_lib="-L$path/lib/python$vers/config -lpython$vers"
	      python_site=$path/lib/python$vers/site-packages
	    fi
	    if test -d $path/lib64/python$vers; then
	      python_lib="-L$path/lib64/python$vers/config -lpython$vers"
	      python_site=$path/lib64/python$vers/site-packages
	    fi
	fi
	dnl This is currently special cased mostly for Windows
	dnl installs, but we only use python_lib for windows anyways
	if test -f $path/python$vers/include/Python.h; then
	  python_inc=$path/python$vers/include
	  python_lib="-L$path/python$vers/libs -lpython$vers"
	  python_site=$path/python$vers/Lib/site-packages
	  break 2
	fi
	if test "x$python_inc" != "xno" -a "x$python_lib" != "xno"; then
	  break 2
	fi
      done
    done
  fi
  if test "x$python_inc" = "xno"; then
    AC_MSG_ERROR(not found)
    PYTHON=
    PYTHON_INC=
    PYTHON_LIB=
    PYTHON_SITE=
  else
    AC_MSG_RESULT(found $python_inc)
    PYTHON=$python_bin
    PYTHON_INC="-I$python_inc"
    PYTHON_LIB=$python_lib
    if test "x$PYTHON_SITE" = "x"; then
	PYTHON_SITE=$python_site
    fi
    BUILD_WRAPPERS="$BUILD_WRAPPERS python"
  fi
fi

dnl perl options
AC_ARG_ENABLE(perl, [  --enable-perl   Enables building of perl module])
AC_ARG_WITH(perl, [  --with-perl=path   Set location of Perl binary], [cs_cv_perl_path="$withval"], [cs_cv_perl_path=no])

if test "x$enable_perl" = "xyes"; then
  AC_MSG_RESULT(Enabling perl module)
  AC_MSG_CHECKING(for perl >= 5.006)
  perl_path=no
  perl_search_path="/neo/opt /usr/local /usr"
  if test $cs_cv_perl_path != "no" -a -x $cs_cv_perl_path; then
    perl_path=$cs_cv_perl_path
  else
    for path in $perl_search_path; do
      if test -x $path/bin/perl; then
	require_error=`$path/bin/perl -e 'require 5.006' 2>&1`
	if test "x$require_error" = "x"; then
	  perl_path=$path/bin/perl
	  break
	fi
      fi
    done
  fi
  if test "x$perl_path" = "xno"; then
    AC_MSG_RESULT(not found)
    PERL=
  else
    AC_MSG_RESULT(found $perl_path)
    PERL="$perl_path"
    BUILD_WRAPPERS="$BUILD_WRAPPERS perl"
  fi
fi

dnl ruby options
AC_ARG_ENABLE(ruby, [  --enable-ruby   Enables building of ruby module])
AC_ARG_WITH(ruby, [  --with-ruby=path   Set location of Ruby binary], [cs_cv_ruby_path="$withval"], [cs_cv_ruby_path=no])

if test "x$enable_ruby" = "xyes"; then
  AC_MSG_RESULT(Enabling ruby module)
  AC_MSG_CHECKING(for ruby)
  ruby_path=no
  ruby_search_path="/neo/opt /usr/local /usr"
  if test $cs_cv_ruby_path != "no" -a -x $cs_cv_ruby_path; then
    ruby_path=$cs_cv_ruby_path
  else
    for path in $ruby_search_path; do
      if test -x $path/bin/ruby; then
	ruby_path=$path/bin/ruby
	break
      fi
    done
  fi
  if test "x$ruby_path" = "xno"; then
    AC_MSG_RESULT(not found)
    RUBY=
  else
    AC_MSG_RESULT(found $ruby_path)
    RUBY="$ruby_path"
    BUILD_WRAPPERS="$BUILD_WRAPPERS ruby"
  fi
fi

dnl java options
#AC_ARG_ENABLE(java, [  --enable-java   Enables building of java module],
#AC_ARG_WITH(java, [  --with-java=path   Set location of J2SDK], [cs_cv_java_path="$withval"], [cs_cv_java_path=no])
#
#if test "x$enable_java" = "xyes"; then
#  AC_MSG_RESULT(Enabling java module)
#  AC_MSG_CHECKING(for j2sdk path)
#  java_path=no
#  if test $cs_cv_java_path != "no" -a -d $cs_cv_java_path; then
#    java_path=$cs_cv_java_path
#  else
#    java_search_path="/neo/opt /usr/local /usr /usr/lib"
#    for path in $java_search_path; do
#      if test -d $path/java/j2sdk; then
#	java_path=$path/java/j2sdk
#	break
#      fi
#      if test -d $path/j2sdk; then
#	java_path=$path/j2sdk
#	break
#      fi
#      possible="$path/java/j2sdk*"
#      for pos_path in $possible; do
#	if test -d $pos_path; then
#	  java_path=$pos_path
#	  break 2
#	fi
#      done
#      possible="$path/j2sdk*"
#      for pos_path in $possible; do
#	if test -d $pos_path; then
#	  java_path=$pos_path
#	  break 2
#	fi
#      done
#    done
#  fi
#  if test "x$java_path" = "xno"; then
#    AC_MSG_RESULT(not found)
#    JAVA_PATH=
#    JAVA_INCLUDE_PATH=
#  else
#    AC_MSG_RESULT(found $java_path)
#    JAVA_PATH="$java_path"
#    JAVAC="$java_path/bin/javac"
#    _ACJNI_JAVAC=$JAVAC
#    AC_JNI_INCLUDE_DIR
#    for JNI_INCLUDE_DIR in $JNI_INCLUDE_DIRS
#    do
#	JAVA_INCLUDE_PATH="$JAVA_INCLUDE_PATH -I$JNI_INCLUDE_DIR"
#    done
#    BUILD_WRAPPERS="$BUILD_WRAPPERS java-jni"
#  fi
#fi

dnl C# options
AC_ARG_ENABLE(csharp, [  --enable-csharp   Enables building of csharp module],
AC_ARG_WITH(csharp, [  --with-csharp=path   Set location of csharp], [cs_cv_csharp_path="$withval"], [cs_cv_csharp_path=no])

if test "x$enable_csharp" = "xyes"; then
  AC_MSG_RESULT(Enabling csharp module)
  AC_MSG_CHECKING(for csharp path)
  csharp_path=no
  if test $cs_cv_csharp_path != "no" -a -d $cs_cv_csharp_path; then
    csharp_path=$cs_cv_csharp_path
  else
    csharp_search_path="/neo/opt /usr/local /usr"
    for path in $csharp_search_path; do
      if test -f $path/bin/mcs; then
	csharp_path=$path
	break
      fi
    done
  fi
  if test "x$csharp_path" = "xno"; then
    AC_MSG_RESULT(not found)
    CSHARP_PATH=
  else
    AC_MSG_RESULT(found $csharp_path/bin/mcs)
    CSHARP_PATH="$csharp_path"
    BUILD_WRAPPERS="$BUILD_WRAPPERS dso csharp"
  fi
fi

# Export
AC_SUBST(RANLIB)
AC_SUBST(AR)
AC_SUBST(MINGW32)
AC_SUBST(APXS_PATH)
AC_SUBST(PERL)
AC_SUBST(RUBY)
AC_SUBST(BUILD_WRAPPERS)
AC_SUBST(JAVA_PATH)
AC_SUBST(JAVA_INCLUDE_PATH)
AC_SUBST(PYTHON)
AC_SUBST(PYTHON_INC)
AC_SUBST(PYTHON_LIB)
AC_SUBST(PYTHON_SITE)
AC_SUBST(CSHARP_PATH)

AC_SUBST(cs_cv_have_lockf)
AC_SUBST(cs_cv_have_pthread)
AC_SUBST(cs_cv_have_snprintf)
AC_SUBST(cs_cv_have_regex)
AC_SUBST(cs_cv_missing)

# the generated files
AC_CONFIG_HEADERS([cs_config.h])
AC_CONFIG_FILES([clearsilver.pc Makefile cs/Makefile cgi/Makefile util/Makefile])

AC_OUTPUT(rules.mk)
